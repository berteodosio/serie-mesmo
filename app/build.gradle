apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.berteodosio.seriemesmo"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0.0"

        multiDexEnabled true

        // Read the API key from ./secure.properties into R.string.tmbd_api_key
        def secureProps = new Properties()
        if (file("../secure.properties").exists()) {
            file("../secure.properties")?.withInputStream { secureProps.load(it) }
        }
        final tmdbApiKey = secureProps.getProperty("TMDB_API_KEY") ?: ""
        buildConfigField "String", "TMDB_API_KEY", "\"${tmdbApiKey}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Core Dependencies
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Test Dependencies
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // MultiDex
    implementation 'androidx.multidex:multidex:2.0.1'

    // AndroidX
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"

    // Kodein
    implementation 'org.kodein.di:kodein-di-generic-jvm:5.2.0'
    implementation 'org.kodein.di:kodein-di-framework-android-support:5.2.0'
    implementation 'org.kodein.di:kodein-di-jx-inject-jvm:5.2.0'

    // RxJava and RxAndroid
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'

    // Google Material Design
    implementation 'com.google.android.material:material:1.3.0'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // Retrofit
    def retrofitVersion = "2.5.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.9.0'
}